// Copyright 2018 GoProtobuf Authors
//
//    Licensed under the Apache License, Version 2.0 (the "License");
//    you may not use this file except in compliance with the License.
//    You may obtain a copy of the License at
//
//        http://www.apache.org/licenses/LICENSE-2.0
//
//    Unless required by applicable law or agreed to in writing, software
//    distributed under the License is distributed on an "AS IS" BASIS,
//    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//    See the License for the specific language governing permissions and
//    limitations under the License.

// This bnf has been derived from https://developers.google.com/protocol-buffers/docs/reference/proto3-spec

// ### Lexical Part ###

// Lexical elements
// Letters and digits

_letter       : 'A' - 'Z' | 'a' - 'z' ;
_decimalDigit : '0' - '9' ;
_octalDigit   : '0' - '7' ;
_hexDigit     : '0' - '9' | 'A' - 'F' | 'a' - 'f' ;

// Identifiers

ident : _letter { _letter | _decimalDigit | '_' } ;

// Integer literals

_decimalLit : ( '0' - '9' ) { _decimalDigit } ;
_octalLit   : '0' { _octalDigit } ;
_hexLit     : '0' ( 'x' | 'X' ) _hexDigit { _hexDigit } ;
intLit      : _decimalLit | _octalLit | _hexLit ;

// Floating-point literals

_decimals : _decimalDigit { _decimalDigit } ;
_exponent : ( 'e' | 'E' ) [ '+' | '-' ] _decimals ;
floatLit  : ( _decimals '.' [ _decimals ] [ _exponent ] | _decimals _exponent | '.' _decimals [ _exponent ] ) | 'i' 'n' 'f' | 'n' 'a' 'n' ;

// Boolean

boolLit : 't' 'r' 'u' 'e' | 'f' 'a' 'l' 's' 'e' ;

// String literals

strLit      : ( '\'' { _charValue } '\'' ) | ( '"' { _charValue } '"' ) ;
// removed | /[^\0\n\\]/ from _charValue
_charValue  : _hexEscape | _octEscape | _charEscape ;
_hexEscape  : '\\' ( 'x' | 'X' ) _hexDigit _hexDigit ;
_octEscape  : '\\' _octalDigit _octalDigit _octalDigit ;
_charEscape : '\\' ( 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '\\' | '\'' | '"' ) ;
quote       : '\'' | '"' ;

// EmptyStatement

emptyStatement : ';' ;

// Comments

_line_comment  : '/' '/' { . } '\n' | '#' { . } '\n' ;
_block_comment : '/' '*' { . | '*' } '*' '/' ;
!comment       : _line_comment | _block_comment ;
!whitespace    : ' ' | '\t' | '\r' | '\n' ;

// ### Syntax Part ###

// << import "github.com/jmarais/goprotobuf/ast" >>

// Proto file
// proto = syntax { import | package | option | topLevelDef | emptyStatement }
Proto
	: Syntax																	<< $0, nil >>
	| Syntax ProtoContents														<< $0, nil >>
	;

ProtoContents
	: ProtoContent																<< $0, nil >>
	| ProtoContents ProtoContent												<< $0, nil >>
	;

ProtoContent
	: Import																	<< $0, nil >>
	| Package																	<< $0, nil >>
	| Option																	<< $0, nil >>
	| TopLevelDef																<< $0, nil >>
	| emptyStatement															<< $0, nil >>
	;

// Syntax
// The syntax statement is used to define the protobuf version.
// syntax = "syntax" "=" quote "proto3" quote ";"
Syntax
	: "syntax" "=" quote "proto3" quote ";"										<< $0, nil >>
	;

// Import Statement
// The import statement is used to import another .proto's definitions.
// import = "import" [ "weak" | "public" ] strLit ";"
Import
	: "import" ImportState strLit ";"											<< $0, nil >>
	;

ImportState
	: "weak"																	<< $0, nil >>
	| "public"																	<< $0, nil >>
	;

// Package
// The package specifier can be used to prevent name clashes between protocol message types.
// package = "package" fullIdent ";"
Package
	: "package" FullIdent ";"													<< $0, nil >>
	;

// Option
// Options can be used in proto files, messages, enums and services. An option can be a protobuf defined option or a custom option. For more information, see Options in the language guide.
// option = "option" optionName  "=" constant ";"
Option
	: "option" OptionName "=" Constant ";"										<< $0, nil >>
	;

// optionName = ( ident | "(" fullIdent ")" ) { "." ident }
OptionName
	: ident																		<< $0, nil >>
	| ident DotRepeatedIdents													<< $0, nil >>
	| "(" FullIdent ")"															<< $0, nil >>
	| "(" FullIdent ")" DotRepeatedIdents										<< $0, nil >>
	;

DotRepeatedIdents
	: "." ident																	<< $0, nil >>
	| DotRepeatedIdents "." ident												<< $0, nil >>
	;

// topLevelDef = message | enum | service
TopLevelDef
	: Message																	<< $0, nil >>
	| Enum																		<< $0, nil >>
	| Service																	<< $0, nil >>
	;

// Fields
// Fields are the basic elements of a protocol buffer message. Fields can be normal fields, oneof fields, or map fields. A field has a type and field number.
// type = "double" | "float" | "int32" | "int64" | "uint32" | "uint64" | "sint32" | "sint64" | "fixed32" | "fixed64" | "sfixed32" | "sfixed64" | "bool" | "string" | "bytes" | messageType | enumType
Type
	: "double"																	<< $0, nil >>
	| "float"																	<< $0, nil >>
	| "int32"																	<< $0, nil >>
	| "int64"																	<< $0, nil >>
	| "uint32"																	<< $0, nil >>
	| "uint64"																	<< $0, nil >>
	| "sint32"																	<< $0, nil >>
	| "sint64"																	<< $0, nil >>
	| "fixed32"																	<< $0, nil >>
	| "fixed64"																	<< $0, nil >>
	| "sfixed32"																<< $0, nil >>
	| "sfixed64"																<< $0, nil >>
	| "bool"																	<< $0, nil >>
	| "string"																	<< $0, nil >>
	| "bytes"																	<< $0, nil >>
	| MessageType																<< $0, nil >>
	| EnumType																	<< $0, nil >>
	;

// fieldNumber = intLit;
FieldNumber
	: intLit																	<< $0, nil >>
	;

// Normal field
// Each field has type, name and field number. It may have field options.
// field = [ "repeated" ] type fieldName "=" fieldNumber [ "[" fieldOptions "]" ] ";"
Field
	: Type FieldName "=" FieldNumber ";"										<< $0, nil >>
	| Type FieldName "=" FieldNumber "[" FieldOptions "]" ";"					<< $0, nil >>
	| "repeated" Type FieldName "=" FieldNumber ";"								<< $0, nil >>
	| "repeated" Type FieldName "=" FieldNumber "[" FieldOptions "]" ";"		<< $0, nil >>
	;

// fieldOptions = fieldOption { ","  fieldOption }
FieldOptions
	: FieldOption																<< $0, nil >>
	| FieldOptions "," FieldOption												<< $0, nil >>
	;

// fieldOption = optionName "=" constant
FieldOption
	: OptionName "=" Constant													<< $0, nil >>
	;

// Oneof and oneof field
// A oneof consists of oneof fields and a oneof name.
// oneof = "oneof" oneofName "{" { oneofField | emptyStatement } "}"
Oneof
	: "oneof" OneofName "{" "}"													<< $0, nil >>
	| "oneof" OneofName "{" RepeatedOneOfFieldOrEmpty "}"						<< $0, nil >>
	;

// oneofField = type fieldName "=" fieldNumber [ "[" fieldOptions "]" ] ";"
RepeatedOneOfFieldOrEmpty
	: OneofField																<< $0, nil >>
	| RepeatedOneOfFieldOrEmpty OneofField										<< $0, nil >>
	| emptyStatement															<< $0, nil >>
	| RepeatedOneOfFieldOrEmpty emptyStatement									<< $0, nil >>
	;

OneofField
	: Type FieldName "=" FieldNumber ";"										<< $0, nil >>
	| Type FieldName "=" FieldNumber "[" FieldOptions "]" ";"					<< $0, nil >>
	;

// Map field
// A map field has a key type, value type, name, and field number. The key type can be any integral or string type.
// mapField = "map" "<" keyType "," type ">" mapName "=" fieldNumber [ "[" fieldOptions "]" ] ";"
MapField
	: "map" "<" KeyType "," Type ">" MapName "=" FieldNumber ";"						<< $0, nil >>
	| "map" "<" KeyType "," Type ">" MapName "=" FieldNumber "[" FieldOptions "]" ";"	<< $0, nil >>
	;

// keyType = "int32" | "int64" | "uint32" | "uint64" | "sint32" | "sint64" | "fixed32" | "fixed64" | "sfixed32" | "sfixed64" | "bool" | "string"
KeyType
	: "int32"																	<< $0, nil >>
	| "int64"																	<< $0, nil >>
	| "uint32"																	<< $0, nil >>
	| "uint64"																	<< $0, nil >>
	| "sint32"																	<< $0, nil >>
	| "sint64"																	<< $0, nil >>
	| "fixed32"																	<< $0, nil >>
	| "fixed64"																	<< $0, nil >>
	| "sfixed32"																<< $0, nil >>
	| "sfixed64"																<< $0, nil >>
	| "bool"																	<< $0, nil >>
	| "string"																	<< $0, nil >>
	;

//Reserved
// Reserved statements declare a range of field numbers or field names that cannot be used in this message.
// reserved = "reserved" ( ranges | fieldNames ) ";"
Reserved
	: "reserved" Ranges ";"														<< $0, nil >>
	| "reserved" FieldNames ";"													<< $0, nil >>
	;

// ranges = range { "," range }
Ranges
	: Range																		<< $0, nil >>
	| Ranges "," Range															<< $0, nil >>
	;

// range =  intLit [ "to" ( intLit | "max" ) ]
Range
	: intLit																	<< $0, nil >>
	| intLit "to" intLit														<< $0, nil >>
	| intLit "to" "max"															<< $0, nil >>
	;

// fieldNames = fieldName { "," fieldName }
FieldNames
	: FieldName																	<< $0, nil >>
	| FieldNames "," FieldName													<< $0, nil >>
	;

// Top Level definitions
// Enum definition
// The enum definition consists of a name and an enum body. The enum body can have options and enum fields. Enum definitions must start with enum value zero.
// enum = "enum" enumName enumBody
Enum
	: "enum" EnumName EnumBody													<< $0, nil >>
	;

// enumBody = "{" { option | enumField | emptyStatement } "}"
EnumBody
	: "{" "}"																	<< $0, nil >>
	| "{" Option "}"															<< $0, nil >>
	| "{" EnumField "}"															<< $0, nil >>
	| "{" emptyStatement "}"													<< $0, nil >>
	;

// enumField = ident "=" intLit [ "[" enumValueOption { ","  enumValueOption } "]" ]";"
EnumField
	: ident "=" intLit ";"														<< $0, nil >>
	| ident "=" intLit "[" EnumValueOptions "]" ";"								<< $0, nil >>
	;


EnumValueOptions
	: EnumValueOption															<< $0, nil >>
	| EnumValueOptions "," EnumValueOption										<< $0, nil >>
	;

// enumValueOption = optionName "=" constant
EnumValueOption
	: OptionName "=" Constant													<< $0, nil >>
	;

// Message definition
// A message consists of a message name and a message body. The message body can have fields, nested enum definitions, nested message definitions, options, oneofs, map fields, and reserved statements.
// message = "message" messageName messageBody
Message
	: "message" MessageName "{" "}"												<< $0, nil >>
	| "message" MessageName "{" MessageBody "}"								<< $0, nil >>
	;

// messageBody = "{" { field | enum | message | option | oneof | mapField | reserved | emptyStatement } "}"
MessageBody
	: MessageContent															<< $0, nil >>
	| MessageBody MessageContent												<< $0, nil >>
	;

MessageContent
	: Field																		<< $0, nil >>
	| Enum																		<< $0, nil >>
	| Message																	<< $0, nil >>
	| Option																	<< $0, nil >>
	| Oneof																		<< $0, nil >>
	| MapField																	<< $0, nil >>
	| Reserved																	<< $0, nil >>
	| emptyStatement															<< $0, nil >>
	;

// Service definition
// service = "service" serviceName "{" { option | rpc | emptyStatement } "}"
Service
	: "service" ServiceName "{" "}"												<< $0, nil >>
	| "service" ServiceName "{" ServiceBodies "}"								<< $0, nil >>
	;

ServiceBodies
	: ServiceBody																<< $0, nil >>
	| ServiceBodies ServiceBody													<< $0, nil >>
	;

ServiceBody
	: Option																	<< $0, nil >>
	| Rpc																		<< $0, nil >>
	| emptyStatement															<< $0, nil >>
	;

// rpc = "rpc" rpcName "(" [ "stream" ] messageType ")" "returns" "(" [ "stream" ] messageType ")" (( "{" {option | emptyStatement } "}" ) | ";")
Rpc
	: "rpc" RpcName "(" MessageType ")" "returns" "(" MessageType ")" RpcBodies						<< $0, nil >>
	| "rpc" RpcName "(" "stream" MessageType ")" "returns" "(" MessageType ")" RpcBodies			<< $0, nil >>
	| "rpc" RpcName "(" MessageType ")" "returns" "(" "stream" MessageType ")" RpcBodies			<< $0, nil >>
	| "rpc" RpcName "(" "stream" MessageType ")" "returns" "(" "stream" MessageType ")" RpcBodies	<< $0, nil >>
	;
// rpc = "rpc" rpcName "(" [ "stream" ] messageType ")" "returns" "(" [ "stream" ] messageType ")"
//       (( "{" {option | emptyStatement } "}" ) | ";")
RpcBodies
	: "{" "}"																	<< $0, nil >>
	| "{" RpcBody "}"															<< $0, nil >>
	| ";"																		<< $0, nil >>
	;

RpcBody
	: Option																	<< $0, nil >>
	| emptyStatement															<< $0, nil >>
	| RpcBody Option															<< $0, nil >>
	| RpcBody emptyStatement													<< $0, nil >>
	;

// Identifiers
// fullIdent = ident { "." ident }
FullIdent
	: ident																		<< $0, nil >>
	| FullIdent "." ident														<< $0, nil >>
	;

// messageName = ident
MessageName
	: ident																		<< $0, nil >>
	;

// enumName = ident
EnumName
	: ident																		<< $0, nil >>
	;

// fieldName = ident
FieldName
	: ident																		<< $0, nil >>
	;

// oneofName = ident
OneofName
	: ident																		<< $0, nil >>
	;

// mapName = ident
MapName
	: ident																		<< $0, nil >>
	;

// serviceName = ident
ServiceName
	: ident																		<< $0, nil >>
	;

// rpcName = ident
RpcName
	: ident																		<< $0, nil >>
	;

// messageType = [ "." ] { ident "." } messageName
MessageType
	: MessageName																<< $0, nil >>
	| Idents MessageName														<< $0, nil >>
	| "." MessageName															<< $0, nil >>
	| "." Idents MessageName													<< $0, nil >>
	;

Idents
	: ident "."																	<< $0, nil >>
	| Idents ident "."															<< $0, nil >>
	;

// enumType = [ "." ] { ident "." } enumName
EnumType
	: EnumName																	<< $0, nil >>
	| "." EnumName																<< $0, nil >>
	| "." Idents EnumName														<< $0, nil >>
	;

// Constant
// constant = fullIdent | ( [ "-" | "+" ] intLit ) | ( [ "-" | "+" ] floatLit ) | strLit | boolLit
Constant
	: FullIdent																	<< $0, nil >>
	| "-" intLit																<< $0, nil >>
	| "+" intLit																<< $0, nil >>
	| "-" floatLit																<< $0, nil >>
	| "+" floatLit																<< $0, nil >>
	| strLit																	<< $0, nil >>
	| boolLit																	<< $0, nil >>
	;
